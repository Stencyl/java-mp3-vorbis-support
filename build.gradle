apply plugin: 'java'
apply plugin: 'maven-publish'

description = 'MP3 and Vorbis SPI support. Combination of JOrbis, VorbisSPI, MP3SPI, and Tritonus-Share.'
group = 'com.stencyl'
sourceCompatibility = 1.8
targetCompatibility = 1.8

version = rootProject.file('version.txt').text.trim()
def isReleaseVersion = project.hasProperty("release")
if(!isReleaseVersion) version = "$version-SNAPSHOT"

repositories {
    mavenCentral()
}

sourceSets {
    examples
}

dependencies {
    implementation group: 'javazoom', name: 'jlayer', version: '1.0.1'
    testCompile 'junit:junit:4.12'
    examplesCompile sourceSets.main.output
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task run(dependsOn: classes, type: JavaExec) {
    if (project.hasProperty('mainClass')) {
        main = mainClass        
    }
    classpath = sourceSets.main.runtimeClasspath + sourceSets.examples.runtimeClasspath
    standardInput = System.in
    workingDir = sourceSets.test.output.resourcesDir
}

task debug(dependsOn: classes, type: JavaExec) {
    if (project.hasProperty('mainClass')) {
        main = mainClass        
    }
    classpath = sourceSets.main.runtimeClasspath + sourceSets.examples.runtimeClasspath
    standardInput = System.in
    workingDir = sourceSets.test.output.resourcesDir
    debug = true
}

if (!sourceSets.test.output.resourcesDir.exists()) {
    sourceSets.test.output.resourcesDir.mkdirs()
}
test.workingDir = sourceSets.test.output.resourcesDir

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            pom {
                name.set("mp3 vorbis support")
                description.set("MP3 SPI and Vorbis SPI support. Combination of JOrbis, VorbisSPI, MP3SPI, and Tritonus-Share.")
                url.set("https://github.com/Stencyl/java-mp3-vorbis-support")
                
                scm {
                    connection.set("scm:git:git://github.com/Stencyl/java-mp3-vorbis-support.git")
                    developerConnection.set("scm:git:ssh://github.com/Stencyl/java-mp3-vorbis-support.git")
                    url.set("https://github.com/Stencyl/java-mp3-vorbis-support")
                }

                licenses {
                    license {
                        name.set("GNU LESSER GENERAL PUBLIC LICENSE, Version 3")
                        url.set("https://www.gnu.org/licenses/lgpl-3.0.txt")
                    }
                }

                developers {
                    developer {
                        id.set("Trilarion")
                        name.set("Trilarion")
                        email.set("trilarion@twelvepm.de")
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name "stencylMavenRepository"
            url = project.getProperty(name + (isReleaseVersion ? "ReleaseUrl" : "SnapshotUrl"))
            credentials(PasswordCredentials)
        }
    }
}